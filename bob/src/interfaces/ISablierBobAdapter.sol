// SPDX-License-Identifier: GPL-3.0-or-later
pragma solidity >=0.8.22;

import { IERC165 } from "@openzeppelin/contracts/utils/introspection/IERC165.sol";
import { UD60x18 } from "@prb/math/src/UD60x18.sol";
import { IComptrollerable } from "@sablier/evm-utils/src/interfaces/IComptrollerable.sol";

/// @title ISablierBobAdapter
/// @notice Base interface for adapters used by the SablierBob protocol for generating yield.
interface ISablierBobAdapter is IComptrollerable, IERC165 {
    /*//////////////////////////////////////////////////////////////////////////
                                       EVENTS
    //////////////////////////////////////////////////////////////////////////*/

    /// @notice Emitted when the comptroller sets a new yield fee.
    /// @param oldFee The previous yield fee as UD60x18.
    /// @param newFee The new yield fee as UD60x18.
    event SetYieldFee(UD60x18 oldFee, UD60x18 newFee);

    /// @notice Emitted when tokens are staked for a user in a vault.
    /// @param vaultId The ID of the vault.
    /// @param user The address of the user.
    /// @param depositAmount The amount of deposit tokens staked.
    /// @param stakedAmount The amount of yield-bearing tokens received.
    event Stake(uint256 indexed vaultId, address indexed user, uint256 depositAmount, uint256 stakedAmount);

    /// @notice Emitted when staked token attribution is transferred between users.
    /// @param vaultId The ID of the vault.
    /// @param from The address transferring the staked tokens.
    /// @param to The address receiving the staked tokens.
    /// @param amount The amount of staked tokens transferred.
    event TransferStakedTokens(uint256 indexed vaultId, address indexed from, address indexed to, uint256 amount);

    /// @notice Emitted when all staked tokens in a vault are converted back to the deposit token.
    /// @param vaultId The ID of the vault.
    /// @param stakedAmount The total amount of yield-bearing tokens unstaked.
    /// @param depositAmount The total amount of deposit tokens received.
    event UnstakeFullAmount(uint256 indexed vaultId, uint256 stakedAmount, uint256 depositAmount);

    /// @notice Emitted when tokens are unstaked for a user exiting within the grace period.
    /// @param vaultId The ID of the vault.
    /// @param user The address of the user.
    /// @param stakedAmount The amount of yield-bearing tokens unstaked.
    /// @param depositAmount The amount of deposit tokens returned.
    event UnstakeForUserWithinGracePeriod(
        uint256 indexed vaultId,
        address indexed user,
        uint256 stakedAmount,
        uint256 depositAmount
    );

    /*//////////////////////////////////////////////////////////////////////////
                               USER-FACING CONSTANTS
    //////////////////////////////////////////////////////////////////////////*/

    /// @notice Returns the maximum yield fee, denominated in UD60x18, where 1e18 = 100%.
    function MAX_FEE() external view returns (UD60x18);

    /// @notice Returns the address of the SablierBob contract.
    function SABLIER_BOB() external view returns (address);

    /// @notice Returns the current global fee on yield for new vaults, denominated in UD60x18, where 1e18 = 100%.
    function feeOnYield() external view returns (UD60x18);

    /*//////////////////////////////////////////////////////////////////////////
                          USER-FACING READ-ONLY FUNCTIONS
    //////////////////////////////////////////////////////////////////////////*/

    /// @notice Calculates the amount to transfer to a user in a settled vault, and the yield fee.
    /// @param vaultId The ID of the vault.
    /// @param user The address of the user.
    /// @param shareBalance The user's share balance in the vault.
    /// @return amountToTransfer The amount to transfer to the user.
    /// @return feeAmount The fee amount taken from the yield.
    function calculateAmountToTransferWithYield(
        uint256 vaultId,
        address user,
        uint128 shareBalance
    )
        external
        view
        returns (uint128 amountToTransfer, uint128 feeAmount);

    /// @notice Returns the total amount of yield-bearing tokens held in a vault.
    /// @param vaultId The ID of the vault.
    /// @return The total amount of yield-bearing tokens in the vault.
    function getTotalYieldBearingTokenBalance(uint256 vaultId) external view returns (uint128);

    /// @notice Returns the yield fee stored for a specific vault.
    /// @param vaultId The ID of the vault.
    /// @return The yield fee for the vault denominated in UD60x18, where 1e18 = 100%.
    function getVaultYieldFee(uint256 vaultId) external view returns (UD60x18);

    /// @notice Returns the amount of yield-bearing tokens held for a specific user in a vault.
    /// @param vaultId The ID of the vault.
    /// @param user The address of the user.
    /// @return The amount of yield-bearing tokens the user has claim to.
    function getYieldBearingTokenBalanceFor(uint256 vaultId, address user) external view returns (uint256);

    /*//////////////////////////////////////////////////////////////////////////
                        USER-FACING STATE-CHANGING FUNCTIONS
    //////////////////////////////////////////////////////////////////////////*/

    /// @notice Register a new vault with the adapter and snapshot the current fee on yield.
    ///
    /// Requirements:
    /// - The caller must be the SablierBob contract.
    ///
    /// @param vaultId The ID of the newly created vault.
    function registerVault(uint256 vaultId) external;

    /// @notice Sets the fee on yield for future vaults.
    ///
    /// @dev Emits a {SetYieldFee} event.
    ///
    /// Notes:
    /// - This only affects future vaults, fee is not updated for existing vaults.
    ///
    /// Requirements:
    /// - The caller must be the comptroller.
    /// - `newFee` must not exceed MAX_FEE.
    ///
    /// @param newFee The new yield fee as UD60x18 where 1e18 = 100%.
    function setYieldFee(UD60x18 newFee) external;

    /// @notice Stakes tokens deposited by a user in a vault, converting them to yield-bearing tokens.
    ///
    /// @dev Emits a {Stake} event.
    ///
    /// Requirements:
    /// - The caller must be the SablierBob contract.
    /// - The tokens must have been transferred to this contract.
    ///
    /// @param vaultId The ID of the vault.
    /// @param user The address of the user depositing the tokens tokens.
    /// @param amount The amount of tokens to stake.
    function stake(uint256 vaultId, address user, uint256 amount) external;

    /// @notice Converts all yield-bearing tokens in a vault back to deposit tokens after settlement.
    ///
    /// @dev Emits an {UnstakeFullAmount} event.
    ///
    /// Notes:
    /// - This should only be called once per vault after settlement.
    ///
    /// Requirements:
    /// - The caller must be the SablierBob contract.
    ///
    /// @param vaultId The ID of the vault.
    /// @return amountReceivedFromUnstaking The total amount of tokens received from unstaking.
    function unstakeFullAmount(uint256 vaultId) external returns (uint128 amountReceivedFromUnstaking);

    /// @notice Unstakes tokens for a user within the grace period.
    ///
    /// @dev Emits an {UnstakeForUserWithinGracePeriod} event.
    ///
    /// Notes:
    /// - No yield fee is charged during grace period unstaking.
    ///
    /// Requirements:
    /// - The caller must be the SablierBob contract.
    ///
    /// @param vaultId The ID of the vault.
    /// @param user The address of the user.
    function unstakeForUserWithinGracePeriod(uint256 vaultId, address user) external;

    /// @notice Updates staked token balance of a user when vault shares are transferred.
    ///
    /// Requirements:
    /// - The caller must be the SablierBob contract.
    ///
    /// @param vaultId The ID of the vault.
    /// @param from The address transferring vault shares.
    /// @param to The address receiving vault shares.
    /// @param shareAmountTransferred The number of vault shares being transferred.
    /// @param userShareBalanceBeforeTransfer The sender's vault share balance before the transfer.
    function updateStakedTokenBalance(
        uint256 vaultId,
        address from,
        address to,
        uint256 shareAmountTransferred,
        uint256 userShareBalanceBeforeTransfer
    )
        external;
}
